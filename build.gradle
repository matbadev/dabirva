// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        // Java environment
        JAVA_TARGET_VERSION = JavaVersion.VERSION_21
        JAVA_MIN_VERSION = JavaVersion.VERSION_11

        // Kotlin environment
        KOTLIN_TARGET_VERSION = "2.1.20"
        KOTLIN_MIN_VERSION_BASE = "1.6"
        KOTLIN_MIN_VERSION = "$KOTLIN_MIN_VERSION_BASE.21"
        KOTLIN_COMPATIBILITY_VALIDATOR_VERSION = "0.17.0"

        // Android environment
        ANDROID_PLUGIN_VERSION = "8.9.2"
        ANDROID_TARGET_VERSION = 35
        ANDROID_MIN_VERSION = 21

        // Main dependencies
        ANDROID_CORE_VERSION = "1.16.0"
        APP_COMPAT_VERSION = "1.7.0"
        RECYCLER_VIEW_VERSION = "1.4.0"
        TIMBER_VERSION = "5.0.1"

        // Test dependencies
        JACOCO_VERSION = "0.8.13"
        JUNIT_4_VERSION = "4.13.2"
        JUNIT_KTX_VERSION = "1.2.1"
        ESPRESSO_VERSION = "3.6.1"
        MOCKITO_VERSION = "5.17.0"

        // Development tools
        MAVEN_PUBLISH_PLUGIN_VERSION = "0.32.0"
    }
}

plugins {
    id "org.jetbrains.kotlin.android" version "$KOTLIN_TARGET_VERSION" apply false
    id "org.jetbrains.kotlin.kapt" version "$KOTLIN_TARGET_VERSION" apply false
    id "org.jetbrains.kotlin.plugin.parcelize" version "$KOTLIN_TARGET_VERSION" apply false
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "$KOTLIN_COMPATIBILITY_VALIDATOR_VERSION"

    id "com.android.application" version "$ANDROID_PLUGIN_VERSION" apply false
    id "com.android.library" version "$ANDROID_PLUGIN_VERSION" apply false

    id "jacoco"
    id "com.vanniktech.maven.publish" version "$MAVEN_PUBLISH_PLUGIN_VERSION" apply false
}

def javaCurrent = JavaVersion.current()
if (javaCurrent != JAVA_TARGET_VERSION) {
    throw new GradleException("This project must be built with JDK $JAVA_TARGET_VERSION but JDK $javaCurrent was provided.")
}

jacoco {
    toolVersion = JACOCO_VERSION
}

// Task name is used as report file name and that must correspond to a pattern recognized by codecov-bash:
// https://github.com/codecov/codecov-bash/blob/master/codecov
task jacocoMergedReportDebug(type: JacocoReport) {
    group = "reporting"
    description = "Collect Jacoco coverage data of dabirva and example module into a single report"

    reports {
        html.required = false
        xml.required = true
        csv.required = false
    }

    sourceDirectories.setFrom fileTree(dir: projectDir, includes: [
            "dabirva/src/main/java/**/*",
    ])
    classDirectories.setFrom fileTree(dir: projectDir, includes: [
            "dabirva/build/tmp/kotlin-classes/debug/**/*",
    ])
    executionData.setFrom fileTree(dir: projectDir, includes: [
            // Unit tests coverage
            "dabirva/build/outputs/**/*.exec",
            "example/build/outputs/**/*.exec",
            // Instrumented tests coverage
            "dabirva/build/outputs/**/*.ec",
            "example/build/outputs/**/*.ec",
    ])
}
