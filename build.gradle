// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        JAVA_VERSION = JavaVersion.VERSION_1_8
        KOTLIN_VERSION = "1.5.10"
        DOKKA_VERSION = "1.4.32"
        ANDROID_GRADLE_PLUGIN_VERSION = "4.2.1"
        JACOCO_VERSION = "0.8.7"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$ANDROID_GRADLE_PLUGIN_VERSION"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$DOKKA_VERSION"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "0.5.0"
    id "jacoco"
}

jacoco {
    toolVersion = JACOCO_VERSION
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    gradle.projectsEvaluated {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) { task ->
            if (!task.name.toLowerCase().contains("debug")) {
                kotlinOptions {
                    allWarningsAsErrors = true
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task jacocoMergedReportDebug(type: JacocoReport) {
    group = "reporting"
    description = "Collect Jacoco coverage data of dabirva and example module into a single report"

    reports {
        xml.enabled = true
        html.enabled = false
        csv.enabled = false
    }

    sourceDirectories.setFrom fileTree(dir: projectDir, includes: [
            "dabirva/src/main/java/**/*",
    ])
    classDirectories.setFrom fileTree(dir: projectDir, includes: [
            "dabirva/build/tmp/kotlin-classes/debug/**/*",
    ])
    executionData.setFrom fileTree(dir: projectDir, includes: [
            // Unit tests coverage
            "dabirva/*.exec",
            "example/*.exec",
            // Instrumented tests coverage
            "dabirva/build/outputs/code_coverage/**/*.ec",
            "example/build/outputs/code_coverage/**/*.ec",
    ])
}
