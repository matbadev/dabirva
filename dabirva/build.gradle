import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "jacoco"
    id "com.vanniktech.maven.publish"
}

ext {
    GROUP_ID = "com.matbadev.dabirva"
    ARTIFACT_ID = "dabirva"
    VERSION_NAME = "1.3.0"
}

jacoco {
    toolVersion = JACOCO_VERSION
}

android {
    namespace "com.matbadev.dabirva"
    compileSdk ANDROID_TARGET_VERSION

    defaultConfig {
        minSdkVersion ANDROID_MIN_VERSION
        targetSdkVersion ANDROID_TARGET_VERSION
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled project.hasProperty("coverage")
        }

        release {
            minifyEnabled false
            testCoverageEnabled false
        }
    }

    buildFeatures {
        buildConfig true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JAVA_MIN_VERSION
        targetCompatibility JAVA_MIN_VERSION
    }

    kotlinOptions {
        jvmTarget = "$JAVA_MIN_VERSION"
        languageVersion = KOTLIN_MIN_VERSION_BASE
        apiVersion = KOTLIN_MIN_VERSION_BASE
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
    }

    testOptions {
        // Save instrumented test results to same directory as unit test results
        resultsDir "$buildDir/test-results"
    }

    // See: https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
    lintOptions {
        abortOnError true
        warningsAsErrors false
        textReport false
        xmlReport true
        htmlReport false
    }
}

dependencies {
    api "androidx.recyclerview:recyclerview:$RECYCLER_VIEW_VERSION"

    // Unit tests
    testImplementation "junit:junit:$JUNIT_4_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_TARGET_VERSION"
    testImplementation "androidx.test.ext:junit-ktx:$JUNIT_KTX_VERSION"
    testImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"

    // Android tests
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_TARGET_VERSION"
    androidTestImplementation "androidx.test.ext:junit-ktx:$JUNIT_KTX_VERSION"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$ESPRESSO_VERSION"
    androidTestImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    coordinates(GROUP_ID, ARTIFACT_ID, VERSION_NAME)

    pom {
        name = "Dabirva"
        description = "Data Binding RecyclerView Adapter for Android"
        url = "https://github.com/matbadev/dabirva"

        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "matbadev"
                name = "Matthias BÃ¤uerle"
                url = "https://www.matbadev.com/"
            }
        }

        scm {
            url = "https://github.com/matbadev/dabirva"
            connection = "scm:git:git://github.com/matbadev/dabirva.git"
            developerConnection = "scm:git:ssh://git@github.com/matbadev/dabirva.git"
        }
    }
}
