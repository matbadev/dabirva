plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "de.mannodermaus.android-junit5"
    id "jacoco"
    id "signing"
    id "maven-publish"
}

ext {
    GROUP_ID = "com.matbadev.dabirva"
    ARTIFACT_ID = "dabirva"
    VERSION_NAME = "1.1.0-SNAPSHOT"
    SNAPSHOT_BUILD = VERSION_NAME.endsWith("-SNAPSHOT")
}

jacoco {
    toolVersion = JACOCO_VERSION
}

android {
    namespace "com.matbadev.dabirva"
    compileSdkVersion ANDROID_TARGET_VERSION

    defaultConfig {
        minSdkVersion ANDROID_MIN_VERSION
        targetSdkVersion ANDROID_TARGET_VERSION
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled project.hasProperty("coverage")
        }

        release {
            minifyEnabled false
            testCoverageEnabled false
        }
    }

    buildFeatures {
        buildConfig true
        dataBinding true
    }

    // Newer JDK target syntax only supported after 8.1.0-alpha09, see:
    // https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }

    kotlinOptions {
        jvmTarget = JAVA_VERSION
        languageVersion = KOTLIN_MIN_VERSION_BASE
        apiVersion = KOTLIN_MIN_VERSION_BASE
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
    }

    testOptions {
        // Save instrumented test results to same directory as unit test results
        resultsDir "$buildDir/test-results"
    }

    // See: https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
    lintOptions {
        abortOnError true
        warningsAsErrors false
        textReport false
        xmlReport true
        htmlReport false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    //implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_MIN_VERSION"

    api "androidx.recyclerview:recyclerview:$RECYCLER_VIEW_VERSION"

    // Unit tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_5_VERSION"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_5_VERSION"
    testImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"

    // Android tests
    androidTestImplementation "androidx.test.ext:junit-ktx:$JUNIT_KTX_VERSION"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$ESPRESSO_VERSION"
    androidTestImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Basic details for the generated POM.
                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION_NAME

                // Metadata for the generated POM.
                pom {
                    name = "Dabirva"
                    description = "Data Binding RecyclerView Adapter for Android"
                    url = "https://github.com/matbadev/dabirva"
                    packaging = "aar"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = "matbadev"
                            name = "Matthias BÃ¤uerle"
                            url = "https://www.matbadev.com/"
                        }
                    }

                    scm {
                        url = "https://github.com/matbadev/dabirva"
                        connection = "scm:git:git://github.com/matbadev/dabirva.git"
                        developerConnection = "scm:git:ssh://github.com:matbadev/dabirva.git"
                    }
                }
            }
        }

        repositories {
            maven {
                name = "centralS01Snapshots"
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = findProperty("publishing.central.s01.username")
                    password = findProperty("publishing.central.s01.password")
                }
            }

            maven {
                name = "centralS01Staging"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("publishing.central.s01.username")
                    password = findProperty("publishing.central.s01.password")
                }
            }
        }
    }

    signing {
        required { !SNAPSHOT_BUILD }
        sign publishing.publications.maven
    }
}

tasks.withType(Sign) {
    onlyIf { !SNAPSHOT_BUILD }
}

// Workaround until AGP 8.1 to specify JDK target version correctly, see:
// https://stackoverflow.com/a/69084853
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }
}
