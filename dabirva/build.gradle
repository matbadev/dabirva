plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-kapt"
    id "de.mannodermaus.android-junit5"
    id "jacoco"
}

ext {
    GROUP_ID = "com.matbadev.dabirva"
    ARTIFACT_ID = "dabriva"
}

jacoco {
    toolVersion = "0.8.6"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 10000
        versionName "1.0.0-alpha1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }

    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
    }

    // CI should run tests in release build type but when working in Android Studio debug should be selected
    testBuildType = project.hasProperty("ci") ? "release" : "debug"

    testOptions {
        // Save instrumented test results to same directory as unit test results
        resultsDir "$buildDir/test-results"
    }

    // See: https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
    lintOptions {
        abortOnError true
        warningsAsErrors true
        textReport false
        xmlReport true
        htmlReport false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

    api "androidx.recyclerview:recyclerview:1.1.0"

    // Unit tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

    // Android tests
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "org.mockito:mockito-android:3.7.7"
}

task mergeJacocoReports(type: JacocoReport) {
    group = "reporting"
    description = "Merge Jacoco coverage reports into a single report"

    reports {
        xml.enabled = true
        html.enabled = false
        csv.enabled = false
    }

    sourceDirectories.setFrom "$projectDir/src/main/java"
    classDirectories.setFrom "$buildDir/tmp/kotlin-classes/release"
    executionData.setFrom fileTree(dir: projectDir, includes: [
            // Unit tests coverage
            "*.exec",
            // Instrumented tests coverage
            "build/outputs/code_coverage/**/*.ec",
    ])
}
