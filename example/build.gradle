plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.plugin.parcelize"
    id "de.mannodermaus.android-junit5"
    id "jacoco"
}

jacoco {
    toolVersion = JACOCO_VERSION
}

android {
    namespace "com.matbadev.dabirva.example"
    compileSdkVersion ANDROID_TARGET_VERSION

    defaultConfig {
        applicationId "com.matbadev.dabirva.example"
        minSdkVersion ANDROID_MIN_VERSION
        targetSdkVersion ANDROID_TARGET_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled project.hasProperty("coverage")
        }

        release {
            minifyEnabled false
            testCoverageEnabled false
        }
    }

    buildFeatures {
        buildConfig true
        dataBinding true
    }

    // Newer JDK target syntax only supported after 8.1.0-alpha09, see:
    // https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }

    kotlinOptions {
        jvmTarget = JAVA_VERSION
        languageVersion = KOTLIN_MIN_VERSION_BASE
        apiVersion = KOTLIN_MIN_VERSION_BASE
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
    }

    testOptions {
        // Save instrumented test results to same directory as unit test results
        resultsDir "$buildDir/test-results"
    }

    // See: https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
    lintOptions {
        abortOnError true
        warningsAsErrors false
        textReport false
        xmlReport true
        htmlReport false
    }
}

dependencies {
    implementation project(":dabirva")

    //implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_MIN_VERSION"

    implementation "androidx.core:core-ktx:$ANDROID_CORE_VERSION"
    implementation "androidx.appcompat:appcompat:$APP_COMPAT_VERSION"

    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"

    // Unit tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_5_VERSION"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_5_VERSION"
    testImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"

    // Android tests
    androidTestImplementation "androidx.test.ext:junit-ktx:$JUNIT_KTX_VERSION"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$ESPRESSO_VERSION"
    androidTestImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"
}

// Workaround until AGP 8.1 to specify JDK target version correctly, see:
// https://stackoverflow.com/a/69084853
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }
}
